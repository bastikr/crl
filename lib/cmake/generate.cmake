cmake_minimum_required(VERSION 3.14)

function(write_header target_name target_path)
    file(WRITE "${target_path}"
"// Generated by crl
#pragma once

#include <crl.h>

#include <string_view>

namespace ${target_name} {

const crl::DirectoryEntry& root();

std::optional<std::span<const std::byte>> get_file(std::string_view path);
constexpr std::span<const std::byte> get_file_static(std::string_view path);

} // namespace ${target_name}
"
    )
endfunction()

function(write_cpp_intro target_path)
    file(WRITE "${target_path}"
"// Generated by crl
#include <array>
#include <cstddef>
#include <crl.h>

using crl::DirectoryEntry;
using crl::FileEntry;

template<typename... Ts>
constexpr std::array<std::byte, sizeof...(Ts)> make_bytes(Ts&&... args) noexcept {
    return{std::byte(std::forward<Ts>(args))...};
}

")
endfunction()

function(write_cpp_outro target_name target_path)
    file(APPEND "${target_path}"
"namespace ${target_name} {

const crl::DirectoryEntry& root() {
    return directory_root;
}

std::optional<std::span<const std::byte>> get_file(std::string_view path) {
    return crl::get_file(root(), path);
}

constexpr std::span<const std::byte> get_file_static(std::string_view path) {
    if (path == \"logs/source/t.txt\") {
        return file_0_data;
    }
    return file_0_data;
    // static_assert(false);
}

} // namespace ${target_name}
")
endfunction()

function(write_data_array input_path variable_name target_path)
    file(READ "${input_path}" file_content HEX)
    string(LENGTH "${file_content}" data_length_hex)
    math(EXPR data_length_bytes "${data_length_hex} / 2")
    file(APPEND "${target_path}" "constexpr std::array<std::byte, ${data_length_bytes}> ${variable_name} = make_bytes(")

    if(data_length_bytes GREATER 0)
        if(data_length_bytes GREATER 1)
            math(EXPR data_length_bytes_minus_two "${data_length_bytes} - 2")
            foreach(i RANGE 0 ${data_length_bytes_minus_two})
                math(EXPR index "2 * ${i}")
                string(SUBSTRING "${file_content}" ${index} 2 BYTE_HEX)
                file(APPEND "${target_path}" "0x${BYTE_HEX},")
            endforeach()
        endif()
        math(EXPR index "2 * ${data_length_bytes} - 2")
        string(SUBSTRING "${file_content}" ${index} 2 BYTE_HEX)
        file(APPEND "${target_path}" "0x${BYTE_HEX}")
    endif()
    file(APPEND "${target_path}" ");\n")
endfunction()

function(write_file_entry file_index file_path target_path)
    cmake_path(GET file_path FILENAME file_name)
    file(APPEND
        "${target_path}"
        "constexpr FileEntry file_${file_index} = { \"${file_name}\", file_${file_index}_data };\n\n"
    )
endfunction()

function(write_file_entries base_dir resource_files target_path)
    set(index 0)
    foreach(resource_file IN LISTS resource_files)
        set(absolute_resource_file "${base_dir}/${resource_file}")
        message(STATUS "Embed ${resource_file} into ${target_path}")
        write_data_array("${absolute_resource_file}" "file_${index}_data" "${target_path}")
        write_file_entry("${index}" "${resource_file}" "${target_path}")
        math(EXPR index "${index} + 1")
    endforeach()
endfunction()


function (add_directory_to_directory_list candidate directory_list out)
    list(FIND directory_list "${candidate}" pos)
    if("${candidate}" IN_LIST directory_list)
    else()
        list(APPEND directory_list "${candidate}")
        set(${out} "${directory_list}" PARENT_SCOPE)
    endif()
endfunction()

function (expand_directories_for_single_file relative_file_path directory_list out)
    get_filename_component(dir_path "${relative_file_path}" DIRECTORY)
    if("${dir_path}" STREQUAL "${relative_file_path}")
        message("Skip file ${dir_path}")
        return()
    endif()
    string(REPLACE "/" ";" dir_part_list "${dir_path}")
    foreach(dir_part IN LISTS dir_part_list)
        set(dir "${dir}/${dir_part}")
        add_directory_to_directory_list("${dir}" "${directory_list}" x)
        set(directory_list "${x}")
    endforeach()
    set(${out} "${directory_list}" PARENT_SCOPE)
endfunction()

function (expand_directories relative_file_list out)
    foreach(relative_file IN LISTS relative_file_list)
        expand_directories_for_single_file("${relative_file}" "${directory_list}" x)
        set(directory_list "${x}")
    endforeach()
    set(${out} "${directory_list}" PARENT_SCOPE)
endfunction()

function(find_children dir expanded_dirs out)
    foreach(candidate IN LISTS "${expanded_dirs}")
        cmake_path(GET candidate PARENT_PATH x)
        if ("${x}" STREQUAL "${dir}")
            list(APPEND result ${candidate})
        endif()
    endforeach()
    set(${out} ${result} PARENT_SCOPE)
endfunction()

function(write_directory_subdirectories dir dir_index subdirectories resource_directories target_file)
    list(LENGTH subdirectories subdirs_length)
    file(APPEND "${target_file}" "constexpr std::array<const DirectoryEntry*, ${subdirs_length}> directory_${dir_index}_subdirectories = {")
    foreach(subdir IN LISTS subdirectories)
        list(FIND resource_directories "${subdir}" index)
        if (${index} EQUAL -1)
            message(FATAL_ERROR "Internal error: directory '${subdir}' is not part of the resource directories: '${resource_directories}'")
        endif()
        file(APPEND "${target_file}" "&directory_${index},")
    endforeach()
    file(APPEND "${target_file}" "};\n")
endfunction()

function(write_directory_subfiles dir dir_index subfiles resource_files target_file)
    list(LENGTH subfiles subfiles_length)
    file(APPEND "${target_file}" "constexpr std::array<const FileEntry*, ${subfiles_length}> directory_${dir_index}_subfiles = {")
    foreach(subfile IN LISTS subfiles)
        list(FIND resource_files "${subfile}" index)
        if (${index} EQUAL -1)
            message(FATAL_ERROR "Internal error: file '${subfile}' is not part of the resource files: '${resource_files}'")
        endif()
        file(APPEND "${target_file}" "&file_${index},")
    endforeach()
    file(APPEND "${target_file}" "};\n")
endfunction()

function(write_directory_entries resource_files resource_directories target_file)
    list(REVERSE resource_directories)
    foreach(dir IN LISTS resource_directories)
        list(FIND resource_directories "${dir}" dir_index)
        if (${dir_index} EQUAL -1)
            message(FATAL_ERROR "Internal error: directory '${dir}' is not part of the resource directories: '${resource_directories}'")
        endif()
        find_children("${dir}" resource_directories subdirs)
        STRING(REGEX REPLACE "^/" "" dir_without_leading_slash "${dir}")
        find_children("${dir_without_leading_slash}" resource_files subfiles)
        write_directory_subdirectories("${dir}" "${dir_index}" "${subdirs}" "${resource_directories}" "${target_file}")
        write_directory_subfiles("${dir}" "${dir_index}" "${subfiles}" "${resource_files}" "${target_file}")
        cmake_path(GET dir FILENAME dir_name)
        file(APPEND "${target_file}" "constexpr DirectoryEntry directory_${dir_index} = { \"${dir_name}\", directory_${dir_index}_subdirectories, directory_${dir_index}_subfiles };\n\n")
    endforeach()
    find_children("/" resource_directories subdirs)
    find_children("" resource_files subfiles)
    write_directory_subdirectories("${dir}" "root" "${subdirs}" "${resource_directories}" "${target_file}")
    write_directory_subfiles("${dir}" "root" "${subfiles}" "${resource_files}" "${target_file}")
    file(APPEND "${target_file}" "constexpr DirectoryEntry directory_root = { \"\", directory_root_subdirectories, directory_root_subfiles };\n\n")
endfunction()

function(main)
    if(NOT IS_DIRECTORY "${SOURCE_DIR}")
        message(FATAL_ERROR "The source directory '${SOURCE_DIR}' provided for the resource library does not exist.")
    endif()
    file(GLOB_RECURSE resource_files RELATIVE "${SOURCE_DIR}" "${SOURCE_DIR}/*")
    expand_directories("${resource_files}" resource_directories)

    set(output_cpp_file "src/${TARGET_NAME}.cpp")
    set(output_h_file "include/${TARGET_NAME}.h")

    write_cpp_intro("${output_cpp_file}")
    write_file_entries("${SOURCE_DIR}" "${resource_files}" "${output_cpp_file}")
    write_directory_entries("${resource_files}" "${resource_directories}" "${output_cpp_file}")
    write_cpp_outro("${TARGET_NAME}" "${output_cpp_file}")

    write_header("${TARGET_NAME}" "${output_h_file}")
endfunction()

main()
